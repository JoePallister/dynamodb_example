resource "aws_apigatewayv2_api" "api" {
  name          = var.api_name
  protocol_type = "HTTP"
}

resource "aws_apigatewayv2_integration" "lambda" {
  api_id = aws_apigatewayv2_api.api.id

  integration_type = "AWS_PROXY"
  integration_uri  = var.lambda_invoke_arn

  payload_format_version = "2.0"
}

# The requests at these route will trigger the lambda
resource "aws_apigatewayv2_route" "get_release" {
  api_id = aws_apigatewayv2_api.api.id

  route_key = "GET /releases/{service}"

  target = "integrations/${aws_apigatewayv2_integration.lambda.id}"
}

resource "aws_apigatewayv2_route" "post_release" {
  api_id = aws_apigatewayv2_api.api.id

  route_key = "POST /releases/{service}"

  target = "integrations/${aws_apigatewayv2_integration.lambda.id}"
}

resource "aws_apigatewayv2_route" "rollback_release" {
  api_id = aws_apigatewayv2_api.api.id

  route_key = "POST /releases/{service}/rollback"

  target = "integrations/${aws_apigatewayv2_integration.lambda.id}"
}

# Allow apigateway to invoke the lambda
resource "aws_lambda_permission" "apigw" {
  statement_id  = "AllowAPIGatewayInvoke"
  action        = "lambda:InvokeFunction"
  function_name = var.lambda_function_name
  principal     = "apigateway.amazonaws.com"

  source_arn = "${aws_apigatewayv2_api.api.execution_arn}/*/*"
}

resource "aws_apigatewayv2_stage" "default" {
  api_id = aws_apigatewayv2_api.api.id

  name        = "$default"
  auto_deploy = true
}
